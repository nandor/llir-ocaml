# 2 "asmcomp/genm/emit.mlp"
(**************************************************************************)
(*                                                                        *)
(*                                 OCaml                                  *)
(*                                                                        *)
(*                 Nandor Licker, University of Cambridge                 *)
(*                                                                        *)
(*   Copyright 2018--     Nandor Licker                                   *)
(*                                                                        *)
(*   All rights reserved.  This file is distributed under the terms of    *)
(*   the GNU Lesser General Public License version 2.1, with the          *)
(*   special exception on linking described in the file LICENSE.          *)
(*                                                                        *)
(**************************************************************************)

(* Specific operations for the GenM processor *)

open Arch
open Cmm
open Mach
open Linearize
open Emitaux
open Reg

type alloc_site = { gc_lbl: label; gc_ret: label }

let emit_symbol s = Emitaux.emit_symbol '_' s

let emit_float f = emit_string (string_of_float f)

let emit_linear fast body =
  let bound_error_label = ref None in
  let gc_sites = ref [] in

  (* Find the next available virtual register *)
  let next_reg = ref 0 in
  let params = Hashtbl.create 10 in
  let rec find_regs i =
    let max_reg = function
      | { loc = Reg r } ->
        let n =  Proc.num_available_registers.(0) in
        if r >= n then begin
          match r land 7 with
          | 0 -> (* argument *) ()
          | 1 -> (* param *)    Hashtbl.replace params r ((r - n) lsr 3)
          | 2 -> (* result *)   ()
          | _ -> assert false
        end else
          next_reg := max (r + 1) !next_reg;
      | { loc = Unknown } -> assert false
      | { loc = Stack _ } -> assert false
    in
    i.arg |> Array.iter max_reg;
    i.res |> Array.iter max_reg;
    if i.desc <> Lend then find_regs i.next
  in
  find_regs body;
  params |> Hashtbl.iter (fun arg idx ->
    `  arg.i64 ${emit_int arg}, {emit_int idx}\n`
  );

  let make_temp () = let n = !next_reg in next_reg := n + 1; n in
  let emit_temp n = emit_string ("$" ^ (string_of_int n)) in

  let emit_const n =
    let reg = make_temp () in
    `  imm.i64 {emit_temp reg}, {emit_int n}\n`;
    reg
  in

  let emit_addr symbol =
    let temp = make_temp () in
    `  addr.i64 {emit_temp temp}, {emit_symbol symbol}\n`;
    temp
  in

  let emit_cc cc =
    let cc_str = match cc with
      | Isigned   Ceq -> "eq"
      | Isigned   Cne -> "neq"
      | Isigned   Clt -> "lt.i64"
      | Isigned   Cgt -> "gt.i64"
      | Isigned   Cle -> "le.i64"
      | Isigned   Cge -> "ge.i64"
      | Iunsigned Ceq -> "eq"
      | Iunsigned Cne -> "neq"
      | Iunsigned Clt -> "ls.u64"
      | Iunsigned Cgt -> "gt.u64"
      | Iunsigned Cle -> "le.u64"
      | Iunsigned Cge -> "ge.u64"
    in emit_string cc_str
  in

  let emit_reg = function
    | {loc = Reg r} ->
      emit_string ("$" ^ (string_of_int r))
    | _ ->
      assert false
  in

  let emit_regs regs =
    let n = Array.length regs in
    Array.iteri (fun i reg ->
      emit_reg reg;
      if i + 1 <> n then emit_string ", "
    ) regs
  in

  let emit_temps temps =
    emit_string (String.concat ", " (List.map (fun n -> "$" ^ string_of_int n) temps))
  in

  let emit_label lbl =
    emit_string ".BB"; emit_int lbl
  in

  let emit_intop res lhs rhs op =
    let emit inst =
      `  {emit_string inst}.i64 {emit_reg res.(0)}, {emit_reg lhs.(0)}, {rhs ()}\n`
    in
    match op with
    | Iadd    -> emit "add"
    | Isub    -> emit "sub"
    | Imul    -> emit "mul"
    | Imulh   -> emit "mulh"
    | Idiv    -> emit "div"
    | Imod    -> emit "mod"
    | Iand    -> emit "and"
    | Ior     -> emit "or"
    | Ixor    -> emit "xor"
    | Ilsl    -> emit "lsl"
    | Ilsr    -> emit "lsr"
    | Iasr    -> emit "asr"
    | Icomp cc ->
        `  set{emit_cc cc} {emit_reg res.(0)}, {emit_reg lhs.(0)}, {rhs ()}\n`;
    | Icheckbound _ ->
      let label = match !bound_error_label with
        | None ->
          let label = new_label () in
          bound_error_label := Some label;
          label
        | Some label -> label
      in
      let flag = make_temp () in
      `  setle.u64 {emit_temp flag}, {emit_reg lhs.(0)}, {rhs ()}\n`;
      `  jt {emit_temp flag}, {emit_label label}\n`
  in

  let emit_op arg res = function
    | Imove   -> assert false
    | Ispill  -> assert false
    | Ireload -> assert false
    | Iconst_int(n) ->
      `  imm.i64 {emit_reg res.(0)}, {emit_int (Nativeint.to_int n)}\n`;
    | Iconst_float(f) ->
      `  imm.f64 {emit_reg res.(0)}, {emit_printf "0x%Lx" f}\n`;
    | Iconst_symbol(s) ->
      `  addr.i64 {emit_reg res.(0)}, {emit_symbol s}\n`;
    | Icall_ind _ ->
      let nres = Array.length res in
      let types = String.concat "" (List.init nres (fun _ -> ".i64")) in
      `  call{emit_string types} {emit_regs res}, {emit_regs arg} @caml_call_frame\n`
    | Icall_imm { func } ->
      let nres = Array.length res in
      let types = String.concat "" (List.init nres (fun _ -> ".i64")) in
      let addr = make_temp () in
      `  addr.i64 {emit_temp addr}, {emit_symbol func}\n`;
      `  call{emit_string types} {emit_regs res}, {emit_temp addr}, {emit_regs arg} @caml_call_frame\n`
    | Itailcall_ind(_) ->
      `  tcall {emit_regs arg}\n`
    | Itailcall_imm { func } ->
      let addr = make_temp () in
      `  addr.i64 {emit_temp addr}, {emit_symbol func}\n`;
      `  tcall {emit_temp addr}, {emit_regs arg}\n`
    | Iextcall { func } ->
      let nres = Array.length res in
      let types = String.concat "" (List.init nres (fun _ -> ".i64")) in
      let addr = make_temp () in
      let wrap = make_temp () in
      `  addr.i64 {emit_temp addr}, {emit_symbol func}\n`;
      `  addr.i64 {emit_temp wrap}, {emit_symbol "caml_c_call"}\n`;
      `  call{emit_string types} {emit_temp wrap}, {emit_temp addr}, {emit_regs arg}\n`
    | Istackoffset(_) ->
      assert false
    | Iload(size, addr) ->
      let instr = match size with
        | Byte_unsigned      -> "1.u64"
        | Byte_signed        -> "1.s64"
        | Sixteen_unsigned   -> "2.u64"
        | Sixteen_signed     -> "2.s64"
        | Thirtytwo_unsigned -> "4.u64"
        | Thirtytwo_signed   -> "4.s64"
        | Word_int           -> "8.u64"
        | Word_val           -> "8.u64"
        | Single             -> "4.f64"
        | Double             -> "8.f64"
        | Double_u           -> "8.f64"
      in
      (match addr with
      | Iindexed(0) ->
        `  ld.{emit_string instr} {emit_reg res.(0)}, [{emit_reg arg.(0)}]\n`
      | Iindexed(n) ->
        let imm = emit_const n in
        let addr = make_temp () in
        `  add.i64 {emit_temp addr}, {emit_reg arg.(0)}, {emit_temp imm}\n`;
        `  ld.{emit_string instr} {emit_reg res.(0)}, [{emit_temp addr}]\n`
      )
    | Istore(size, addr, _) ->
      let instr = match size with
        | Byte_unsigned      | Byte_signed        -> "1.i64"
        | Sixteen_unsigned   | Sixteen_signed     -> "2.i64"
        | Thirtytwo_unsigned | Thirtytwo_signed   -> "4.i64"
        | Word_int           | Word_val           -> "8.i64"
        | Single   -> "8.f32"
        | Double   -> "8.f64"
        | Double_u -> "8.f64"
      in
      (match addr with
      | Iindexed(0) ->
        `  st.{emit_string instr} [{emit_reg arg.(1)}], {emit_reg arg.(0)}\n`
      | Iindexed(n) ->
        let imm = emit_const n in
        let addr = make_temp () in
        `  add.i64 {emit_temp addr}, {emit_reg arg.(1)}, {emit_temp imm}\n`;
        `  st.{emit_string instr} [{emit_temp addr}], {emit_reg arg.(0)}\n`
      );
    | Ialloc { words } ->
      if fast then begin
        let gc_ret = new_label () in
        let gc_lbl = new_label () in
        `{emit_label gc_ret}:\n`;
        let young_lim = make_temp () in
        let young_ptr = make_temp () in
        let new_ptr = make_temp () in
        let flag = make_temp () in
        let immw = emit_const words in
        let addr_young_lim = emit_addr "caml_young_limit" in
        let addr_young_ptr = emit_addr "caml_young_ptr" in
        `  ld.8.i64 {emit_temp young_lim}, [{emit_temp addr_young_lim}]\n`;
        `  ld.8.i64 {emit_temp young_ptr}, [{emit_temp addr_young_ptr}]\n`;
        `  sub.i64 {emit_temp new_ptr}, {emit_temp young_ptr}, {emit_temp immw}\n`;
        `  setlt.i64 {emit_temp flag}, {emit_temp new_ptr}, {emit_temp young_lim}\n`;
        `  jt {emit_temp flag}, {emit_label gc_lbl}\n`;
        let imm8 = emit_const 8 in
        `  add.i64 {emit_reg res.(0)}, {emit_temp young_ptr}, {emit_temp imm8}\n`;
        gc_sites := { gc_lbl; gc_ret } :: !gc_sites
      end else begin
        let ptr = make_temp () in
        let args = match words with
          | 16 -> [emit_addr "caml_alloc1"]
          | 24 -> [emit_addr "caml_alloc2"]
          | 32 -> [emit_addr "caml_alloc3"]
          | _  -> [emit_addr "caml_allocN"; emit_const words]
        in
        `  call.i64 {emit_temp ptr}, {emit_temps args} @caml_call_frame\n`;
        let imm = emit_const 8 in
        `  add.i64 {emit_reg res.(0)}, {emit_temp ptr}, {emit_temp imm}\n`;
      end
    | Iintop(op) ->
      emit_intop res arg (fun () -> emit_reg arg.(1)) op
    | Iintop_imm(op, n) ->
      let imm = emit_const n in
      emit_intop res arg (fun () -> emit_temp imm) op
    | Inegf ->
      `  neg.f64 {emit_reg res.(0)}, {emit_reg arg.(0)}\n`;
    | Iabsf ->
      `  abs.f64 {emit_reg res.(0)}, {emit_reg arg.(0)}\n`;
    | Iaddf ->
      `  add.f64 {emit_reg res.(0)}, {emit_reg arg.(0)}, {emit_reg arg.(1)}\n`;
    | Isubf ->
      `  sub.f64 {emit_reg res.(0)}, {emit_reg arg.(0)}, {emit_reg arg.(1)}\n`;
    | Imulf ->
      `  mul.f64 {emit_reg res.(0)}, {emit_reg arg.(0)}, {emit_reg arg.(1)}\n`;
    | Idivf ->
      `  div.f64 {emit_reg res.(0)}, {emit_reg arg.(0)}, {emit_reg arg.(1)}\n`;
    | Ifloatofint ->
      `  mov.f64 {emit_reg res.(0)}, {emit_reg arg.(0)}\n`;
    | Iintoffloat ->
      `  mov.i64 {emit_reg res.(0)}, {emit_reg arg.(0)}\n`;
    | Ispecific(_) ->
      assert false
    | Iname_for_debugger(_) ->
      assert false
  in
  let emit_instr arg res = function
    | Lend ->
      ()
    | Lop(Imove) ->
      `  mov.i64 {emit_reg res.(0)}, {emit_reg arg.(0)}\n`
    | Lop(Iconst_int n) ->
      `  imm.i64 {emit_reg res.(0)}, {emit_int (Nativeint.to_int n)}\n`
    | Lop(Iconst_symbol s) ->
      `  addr.i64 {emit_reg res.(0)}, {emit_symbol s}\n`
    | Lop(op) ->
      emit_op arg res op
    | Lreloadretaddr ->
      ()
    | Lreturn ->
      let nargs = Array.length arg in
      let types = String.concat "" (List.init nargs (fun _ -> ".i64")) in
      `  ret{emit_string types} {emit_regs arg}\n`;
    | Llabel(label) ->
      `{emit_label label}:\n`
    | Lbranch(label) ->
      `  jmp {emit_label label}\n`
    | Lcondbranch(test, label) ->
      (match test with
      | Itruetest ->
        let flag = make_temp () in
        `  trunc.i32.i64 {emit_temp flag}, {emit_reg arg.(0)}\n`;
        `  jt {emit_temp flag}, {emit_label label}\n`
      | Ifalsetest ->
        let flag = make_temp () in
        `  trunc.i32.i64 {emit_temp flag}, {emit_reg arg.(0)}\n`;
        `  jf {emit_temp flag}, {emit_label label}\n`
      | Iinttest cc ->
        let flag = make_temp () in
        `  set{emit_cc cc} {emit_temp flag}, {emit_reg arg.(0)}, {emit_reg arg.(1)}\n`;
        `  jt {emit_temp flag}, {emit_label label}\n`
      | Iinttest_imm(cc, n) ->
        let imm = emit_const n in
        let flag = make_temp () in
        `  set{emit_cc cc} {emit_temp flag}, {emit_reg arg.(0)}, {emit_temp imm}\n`;
        `  jt {emit_temp flag}, {emit_label label}\n`
      | Ifloattest(cc) ->
        let cond = match cc with
          | CFeq  -> "eq"
          | CFneq -> "neq"
          | CFlt  -> "lt"
          | CFnlt -> "nlt"
          | CFgt  -> "gt"
          | CFngt -> "ngt"
          | CFle  -> "le"
          | CFnle -> "nle"
          | CFge  -> "ge"
          | CFnge -> "nge"
        in
        let flag = make_temp () in
        `  set{emit_string cond}.f64 {emit_temp flag}, {emit_reg arg.(0)}, {emit_reg arg.(1)}\n`;
        `  jt {emit_temp flag}, {emit_label label}\n`
      | Ioddtest ->
        let imm = emit_const 1 in
        let flag = make_temp () in
        `  and.i64 {emit_temp flag}, {emit_reg arg.(0)}, {emit_temp imm}\n`;
        `  jt {emit_temp flag}, {emit_label label}\n`
      | Ieventest ->
        let imm = emit_const 1 in
        let flag = make_temp () in
        `  and.i64 {emit_temp flag}, {emit_reg arg.(0)}, {emit_temp imm}\n`;
        `  jf {emit_temp flag}, {emit_label label}\n`
      );
    | Lcondbranch3(lbl_lt, lbl_eq, lbl_gt) ->
        let imm = emit_const 1 in
        let flag = make_temp () in
        let emit cc lbl =
          `  set{emit_string cc}.u64 {emit_temp flag}, {emit_reg arg.(0)}, {emit_temp imm}\n`;
          `  jt {emit_temp flag}, {emit_label lbl}\n`
        in
        lbl_lt |> Option.iter (emit "lt");
        lbl_eq |> Option.iter (emit "eq");
        lbl_gt |> Option.iter (emit "gt")
    | Lswitch(labels) ->
      `  switch {emit_reg arg.(0)}, [\n`;
      let n = Array.length labels in
      Array.iteri (fun i l ->
        `    {emit_label l}`;
        if i + 1 <> n then `,\n` else `\n`
      ) labels;
      `  ]\n`
    | Lsetuptrap(label) ->
      let lbl = new_label () in
      let addr = make_temp () in
      `  addr.i64 {emit_temp addr}, {emit_label lbl}\n`;
      `  push.i64 {emit_temp addr}\n`;
      `  jmp {emit_label label}\n`
    | Lpushtrap ->
      let exc_addr = emit_addr "caml_exception_pointer" in
      let exc_val = make_temp () in
      `  ld.8.u64 {emit_temp exc_val}, [{emit_temp exc_addr}]\n`;
      `  push.i64 {emit_temp exc_val}\n`;
      `  st.8.u64 [{emit_temp exc_addr}], $sp\n`
    | Lpoptrap ->
      let exc_addr = emit_addr "caml_exception_pointer" in
      let exc_val = make_temp () in
      let dsc_val = make_temp () in
      `  pop.i64 {emit_temp exc_val}\n`;
      `  st.8.u64 [{emit_temp exc_addr}], {emit_temp exc_val}\n`;
      `  pop.i64 {emit_temp dsc_val}\n`
    | Lraise(raise) ->
      (match raise with
      | Cmm.Raise_withtrace ->
        let exn = emit_addr "caml_raise_exn" in
        `  tcall {emit_temp exn}, {emit_regs arg} @caml_raise_frame\n`;
      | Cmm.Raise_notrace ->
        let exc_val = make_temp () in
        let ret_val = make_temp () in
        let exc_addr = emit_addr "caml_exception_pointer" in
        `  ld.8.u64 $sp, [{emit_temp exc_addr}]\n`;
        `  pop.i64 {emit_temp exc_val}\n`;
        `  st.8.u64 [{emit_temp exc_addr}], {emit_temp exc_val}\n`;
        `  pop.i64 {emit_temp ret_val}\n`;
        `  ji {emit_temp ret_val}\n`
      )
  in
  let rec emit = function
    | { desc = Lend } -> ()
    | instr ->
      emit_instr instr.arg instr.res instr.desc;
      emit instr.next
  in
  emit body;
  (* Emit calls to all GC sites *)
  !gc_sites |> List.iter (fun { gc_lbl; gc_ret } ->
    `{emit_label gc_lbl}:\n`;
    let addr = emit_addr "caml_call_gc" in
    `  call {emit_temp addr} @caml_gc_frame\n`;
    `  jmp {emit_label gc_ret}\n`
  );

  (* Emit bound error call *)
  !bound_error_label |> Option.iter (fun label ->
    `{emit_label label}:\n`;
    let addr = emit_addr "caml_ml_array_bound_error" in
    `  tcall {emit_temp addr}\n`;
  )

let data items =
  `  .data\n`;
  `  .align 3\n`;
  items |> List.iter (function
  | Cstring s          -> emit_string_directive "  .ascii  " s
  | Cskip n            -> if n > 0 then `  .space   {emit_int n}\n`
  | Cdefine_symbol s   -> `{emit_symbol s}:\n`
  | Cglobal_symbol s   -> `  .globl   {emit_symbol s}\n`
  | Calign n           -> `  .align   {emit_int n}\n`
  | Cint8 n            -> `  .int8    {emit_int n}\n`
  | Cint16 n           -> `  .int16   {emit_int n}\n`
  | Cint32 n           -> `  .int32   {emit_int (Nativeint.to_int n)}\n`
  | Cint n             -> `  .int64   {emit_int (Nativeint.to_int n)}\n`
  | Csymbol_address s  -> `  .addr    {emit_symbol s}\n`
  | Csingle f          -> `  .float32 {emit_float f}\n`
  | Cdouble f          -> `  .float64 {emit_float f}\n`
  );
  `\n`

let fundecl f =
  `  .text\n`;
  `  .globl {emit_symbol f.fun_name}\n`;
  `{emit_symbol f.fun_name}:\n`;
  emit_linear f.fun_fast f.fun_body;
  `\n`

let begin_assembly () =
  ``

let end_assembly () =
  ``
