# 2 "asmcomp/genm/emit.mlp"
(**************************************************************************)
(*                                                                        *)
(*                                 OCaml                                  *)
(*                                                                        *)
(*                 Nandor Licker, University of Cambridge                 *)
(*                                                                        *)
(*   Copyright 2018--     Nandor Licker                                   *)
(*                                                                        *)
(*   All rights reserved.  This file is distributed under the terms of    *)
(*   the GNU Lesser General Public License version 2.1, with the          *)
(*   special exception on linking described in the file LICENSE.          *)
(*                                                                        *)
(**************************************************************************)

(* Specific operations for the GenM processor *)

open Arch
open Cmm
open Mach
open Linearize
open Emitaux
open Reg

let emit_symbol s = Emitaux.emit_symbol '$' s

let emit_label lbl = emit_string ".L"; emit_int lbl

let emit_linear body =
  let bound_error_labels = ref [] in

  (* Find the next available virtual register *)
  let next_reg = ref 15000 in
  let params = Hashtbl.create 10 in
  let rec find_regs i =
    let max_reg = function
      | { loc = Reg r; typ } ->
        let n =  10000 in
        if r >= n then begin
          match r land 7 with
          | 0 -> (* argument *) ()
          | 1 -> (* param *)    Hashtbl.replace params r (((r - n) lsr 3), typ)
          | 2 -> (* result *)   ()
          | _ -> assert false
        end else
          next_reg := max (r + 1) !next_reg;
      | { loc = Unknown } -> assert false
      | { loc = Stack _ } -> assert false
    in
    i.arg |> Array.iter max_reg;
    i.res |> Array.iter max_reg;
    if i.desc <> Lend then find_regs i.next
  in

  let make_temp () = let n = !next_reg in next_reg := n + 1; n in
  let emit_temp n = emit_string ("$" ^ (string_of_int n)) in

  let emit_const n =
    let reg = make_temp () in
    `  mov.i64 {emit_temp reg}, {emit_int n}\n`;
    reg
  in

  let emit_addr symbol =
    let temp = make_temp () in
    `  mov.i64 {emit_temp temp}, {emit_symbol symbol}\n`;
    temp
  in

  let emit_cc cc =
    let cc_str = match cc with
      | Isigned   Ceq -> "eq"
      | Isigned   Cne -> "ne"
      | Isigned   Clt -> "lt"
      | Isigned   Cgt -> "gt"
      | Isigned   Cle -> "le"
      | Isigned   Cge -> "ge"
      | Iunsigned Ceq -> "eq"
      | Iunsigned Cne -> "ne"
      | Iunsigned Clt -> "uls"
      | Iunsigned Cgt -> "ugt"
      | Iunsigned Cle -> "ule"
      | Iunsigned Cge -> "uge"
    in emit_string cc_str
  in

  let emit_reg = function
    | {loc = Reg r} ->
      emit_string ("$" ^ (string_of_int r))
    | _ ->
      assert false
  in

  let emit_type = function
    | Float-> emit_string "f64"
    | Val -> emit_string "i64"
    | Addr -> emit_string "i64"
    | Int -> emit_string "i64"
  in

  let emit_regs regs st en =
    match regs with
    | [| |] -> ()
    | regs ->
      emit_string st;
      let n = Array.length regs in
      Array.iteri (fun i reg ->
        emit_reg reg;
        if i + 1 <> n then emit_string ", "
      ) regs;
      emit_string en
  in

  let emit_temps temps =
    emit_string (String.concat ", " (List.map (fun n -> "$" ^ string_of_int n) temps))
  in

  let emit_value res = match res with
    | [| |] -> ()
    | [| { typ = Val } |] -> `\t@caml_value`
    | [| { typ = Addr } |] -> `\t@caml_addr`
    | [| { typ = _ } |] -> ()
    | _ -> assert false
  in

  let call_types res =
    String.concat ""
      (List.map
        (fun reg -> match reg.typ with
        | Float -> ".f64"
        | _ -> ".i64"
        )
        (Array.to_list res)
      )
  in

  (* Exception handler stack *)
  let current_block = ref 0 in
  let handlers = ref [] in
  let traps = Hashtbl.create 16 in
  let rec no_handler = function
    | [] -> true
    | Some _ :: _ -> false
    | None :: hs ->  no_handler hs
  in

  let rec emit_call = function
    | Some _ :: _ -> emit_string "invoke"
    | None :: hs -> emit_call hs
    | [] -> emit_string "call"
  in

  let rec emit_handler = function
    | Some h :: _ -> `, {emit_label h}`
    | None :: hs -> emit_handler hs
    | [] -> ()
  in

  let emit_intop res lhs rhs op =
    let emit inst =
      `  {emit_string inst}.i64 {emit_reg res.(0)}, {emit_reg lhs.(0)}, {rhs ()}{emit_value res}\n`
    in
    match op with
    | Iadd    -> emit "add"
    | Isub    -> emit "sub"
    | Imul    -> emit "mul"
    | Imulh   ->
      let rega = make_temp () in
      let regb = make_temp () in
      let regr = make_temp () in
      let regc = make_temp () in
      `  sext.i128 {emit_temp rega}, {emit_reg lhs.(0)}\n`;
      `  sext.i128 {emit_temp regb}, {rhs ()}\n`;
      `  mul.i128 {emit_temp regr}, {emit_temp rega}, {emit_temp regb}\n`;
      `  mov.i8 {emit_temp regc}, 64\n`;
      `  srl.i128 {emit_temp regr}, {emit_temp regr}, {emit_temp regc}\n`;
      `  trunc.i64 {emit_reg res.(0)}, {emit_temp regr}\n`
    | Idiv    -> emit "div"
    | Imod    -> emit "rem"
    | Iand    -> emit "and"
    | Ior     -> emit "or"
    | Ixor    -> emit "xor"
    | Ilsl    -> emit "sll"
    | Ilsr    -> emit "srl"
    | Iasr    -> emit "sra"
    | Icomp cc ->
        `  cmp.{emit_cc cc}.i64 {emit_reg res.(0)}, {emit_reg lhs.(0)}, {rhs ()}\n`;
    | Icheckbound _ ->
      let handler = match !handlers with [] -> None | h :: _ -> h in
      let label = match List.assoc_opt handler !bound_error_labels with
        | None ->
          let label = new_label () in
          bound_error_labels := (handler, label) :: !bound_error_labels;
          label
        | Some label ->
          label
      in
      let flag = make_temp () in
      `  cmp.ule.i32 {emit_temp flag}, {emit_reg lhs.(0)}, {rhs ()}\n`;
      `  jt {emit_temp flag}, {emit_label label}\n`
  in

  let emit_op arg res = function
    | Imove ->
      `  mov.{emit_type res.(0).typ} {emit_reg res.(0)}, {emit_reg arg.(0)}{emit_value res}\n`
    | Ispill  -> assert false
    | Ireload -> assert false
    | Iconst_int(n) ->
      `  mov.i64 {emit_reg res.(0)}, {emit_printf "0x%nx" n}{emit_value res}\n`;
    | Iconst_float(f) ->
      `  mov.f64 {emit_reg res.(0)}, {emit_printf "0x%Lx" f}\n`;
    | Iconst_symbol(s) ->
      `  mov.i64 {emit_reg res.(0)}, {emit_symbol s}{emit_value res}\n`;
    | Icall_ind _ ->
      let types = call_types res in
      `  {emit_call !handlers}{emit_string types}.caml{emit_regs res " " ","} {emit_regs arg " " ""}{emit_handler !handlers} @caml_frame{emit_value res}\n`
    | Icall_imm { func } ->
      let types = call_types res in
      let addr = make_temp () in
      `  mov.i64 {emit_temp addr}, {emit_symbol func}\n`;
      `  {emit_call !handlers}{emit_string types}.caml{emit_regs res " " ","} {emit_temp addr}{emit_regs arg ", """}{emit_handler !handlers} @caml_frame{emit_value res}\n`
    | Itailcall_ind(_) ->
      `  tcall.i64.caml {emit_regs arg " " ""}\n`
    | Itailcall_imm { func } ->
      let addr = make_temp () in
      `  mov.i64 {emit_temp addr}, {emit_symbol func}\n`;
      `  tcall.i64.caml {emit_temp addr}{emit_regs arg ", " ""}\n`
    | Iextcall { func; alloc } ->
      let res_types = call_types res in
      let addr = make_temp () in
      `  mov.i64 {emit_temp addr}, {emit_symbol func}\n`;
      if alloc then begin
        let wrap = make_temp () in
        let arg_types = arg
          |> Array.map (fun arg -> match arg.typ with
            | Float -> "f64"
            | _ -> "i64"
            )
          |> Array.to_list
          |> String.concat "_"
        in
        `  mov.i64 {emit_temp wrap}, {emit_symbol "caml_c_call_"}{emit_string arg_types}\n`;
        `  {emit_call !handlers}{emit_string res_types}.caml_ext{emit_regs res " " ","} {emit_temp wrap}, {emit_temp addr}{emit_regs arg ", " ""}{emit_handler !handlers}  @caml_frame{emit_value res}\n`
      end else begin
        `  {emit_call !handlers}{emit_string res_types}.c{emit_regs res " " ","} {emit_temp addr}{emit_regs arg ", " ""}{emit_handler !handlers}{emit_value res}\n`
      end
    | Istackoffset(_) ->
      assert false
    | Iload(size, addr) ->
      let instr = match size with
        | Byte_unsigned      -> "1.u64"
        | Byte_signed        -> "1.i64"
        | Sixteen_unsigned   -> "2.u64"
        | Sixteen_signed     -> "2.i64"
        | Thirtytwo_unsigned -> "4.u64"
        | Thirtytwo_signed   -> "4.i64"
        | Word_int           -> "8.u64"
        | Word_val           -> "8.u64"
        | Single             -> "4.f64"
        | Double             -> "8.f64"
        | Double_u           -> "8.f64"
      in
      (match addr with
      | Iindexed(0) ->
        `  ld.{emit_string instr} {emit_reg res.(0)}, [{emit_reg arg.(0)}]{emit_value res}\n`
      | Iindexed(n) ->
        let imm = emit_const n in
        let addr = make_temp () in
        `  add.i64 {emit_temp addr}, {emit_reg arg.(0)}, {emit_temp imm}\n`;
        `  ld.{emit_string instr} {emit_reg res.(0)}, [{emit_temp addr}]{emit_value res}\n`
      )
    | Istore(size, addr, _) ->
      let instr = match size with
        | Byte_unsigned      | Byte_signed        -> "1"
        | Sixteen_unsigned   | Sixteen_signed     -> "2"
        | Thirtytwo_unsigned | Thirtytwo_signed   -> "4"
        | Word_int           | Word_val           -> "8"
        | Single   -> "4"
        | Double   -> "8"
        | Double_u -> "8"
      in
      (match addr with
      | Iindexed(0) ->
        `  st.{emit_string instr} [{emit_reg arg.(1)}], {emit_reg arg.(0)}\n`
      | Iindexed(n) ->
        let imm = emit_const n in
        let addr = make_temp () in
        `  add.i64 {emit_temp addr}, {emit_reg arg.(1)}, {emit_temp imm}\n`;
        `  st.{emit_string instr} [{emit_temp addr}], {emit_reg arg.(0)}\n`
      );
    | Ialloc { words } ->
      let args = match words with
        | 16 -> [emit_addr "caml_alloc1"]
        | 24 -> [emit_addr "caml_alloc2"]
        | 32 -> [emit_addr "caml_alloc3"]
        | _  -> [emit_addr "caml_allocN"; emit_const words]
      in
      `  {emit_call !handlers}.i64.caml_alloc {emit_reg res.(0)}, {emit_temps args}{emit_handler !handlers} @caml_frame{emit_value res}\n`
    | Iintop(op) ->
      emit_intop res arg (fun () -> emit_reg arg.(1)) op
    | Iintop_imm(op, n) ->
      let imm = emit_const n in
      emit_intop res arg (fun () -> emit_temp imm) op
    | Inegf ->
      `  neg.f64 {emit_reg res.(0)}, {emit_reg arg.(0)}\n`;
    | Iabsf ->
      `  abs.f64 {emit_reg res.(0)}, {emit_reg arg.(0)}\n`;
    | Iaddf ->
      `  add.f64 {emit_reg res.(0)}, {emit_reg arg.(0)}, {emit_reg arg.(1)}\n`;
    | Isubf ->
      `  sub.f64 {emit_reg res.(0)}, {emit_reg arg.(0)}, {emit_reg arg.(1)}\n`;
    | Imulf ->
      `  mul.f64 {emit_reg res.(0)}, {emit_reg arg.(0)}, {emit_reg arg.(1)}\n`;
    | Idivf ->
      `  div.f64 {emit_reg res.(0)}, {emit_reg arg.(0)}, {emit_reg arg.(1)}\n`;
    | Ifloatofint ->
      `  sext.f64 {emit_reg res.(0)}, {emit_reg arg.(0)}\n`;
    | Iintoffloat ->
      `  trunc.i64 {emit_reg res.(0)}, {emit_reg arg.(0)}\n`;
    | Ispecific(_) ->
      assert false
    | Iname_for_debugger(_) ->
      assert false
  in

  let emit_instr arg res = function
    | Lend ->
      ()
    | Lop(op) ->
      emit_op arg res op
    | Lreloadretaddr ->
      ()
    | Lreturn ->
      `  ret {emit_regs arg " " ""}\n`;
    | Llabel(label) ->
      current_block := label;
      `{emit_label label}:\n`
    | Lbranch(label) ->
      `  jmp {emit_label label}\n`
    | Lcondbranch(test, label) ->
      (match test with
      | Itruetest ->
        let flag = make_temp () in
        `  trunc.i32.i64 {emit_temp flag}, {emit_reg arg.(0)}\n`;
        `  jt {emit_temp flag}, {emit_label label}\n`
      | Ifalsetest ->
        let flag = make_temp () in
        `  trunc.i32.i64 {emit_temp flag}, {emit_reg arg.(0)}\n`;
        `  jf {emit_temp flag}, {emit_label label}\n`
      | Iinttest cc ->
        let flag = make_temp () in
        `  cmp.{emit_cc cc}.i32 {emit_temp flag}, {emit_reg arg.(0)}, {emit_reg arg.(1)}\n`;
        `  jt {emit_temp flag}, {emit_label label}\n`
      | Iinttest_imm(cc, n) ->
        let imm = emit_const n in
        let flag = make_temp () in
        `  cmp.{emit_cc cc}.i32 {emit_temp flag}, {emit_reg arg.(0)}, {emit_temp imm}\n`;
        `  jt {emit_temp flag}, {emit_label label}\n`
      | Ifloattest(cc) ->
        let cond = match cc with
          | CFeq  -> "oeq"
          | CFneq -> "une"
          | CFlt  -> "olt"
          | CFnlt -> "ugt"
          | CFgt  -> "ogt"
          | CFngt -> "ule"
          | CFle  -> "ole"
          | CFnle -> "ugt"
          | CFge  -> "oge"
          | CFnge -> "ult"
        in
        let flag = make_temp () in
        `  cmp.{emit_string cond}.i32 {emit_temp flag}, {emit_reg arg.(0)}, {emit_reg arg.(1)}\n`;
        `  jt {emit_temp flag}, {emit_label label}\n`
      | Ioddtest ->
        let imm = emit_const 1 in
        let flag = make_temp () in
        `  and.i64 {emit_temp flag}, {emit_reg arg.(0)}, {emit_temp imm}\n`;
        `  jt {emit_temp flag}, {emit_label label}\n`
      | Ieventest ->
        let imm = emit_const 1 in
        let flag = make_temp () in
        `  and.i64 {emit_temp flag}, {emit_reg arg.(0)}, {emit_temp imm}\n`;
        `  jf {emit_temp flag}, {emit_label label}\n`
      );
    | Lcondbranch3(lbl_lt, lbl_eq, lbl_gt) ->
        let imm = emit_const 1 in
        let flag = make_temp () in
        let emit cc lbl =
          `  cmp.{emit_string cc}.i32 {emit_temp flag}, {emit_reg arg.(0)}, {emit_temp imm}\n`;
          `  jt {emit_temp flag}, {emit_label lbl}\n`
        in
        lbl_lt |> Option.iter (emit "lt");
        lbl_eq |> Option.iter (emit "eq");
        lbl_gt |> Option.iter (emit "gt")
    | Lswitch(labels) ->
      `  switch {emit_reg arg.(0)}, `;
      let n = Array.length labels in
      Array.iteri (fun i l ->
        `{emit_label l}`;
        if i + 1 <> n then `, ` else ()
      ) labels;
      `\n`
    | Lsetuptrap(label) ->
      let handler = new_label () in
      Hashtbl.add traps label handler;
      let exn_addr = make_temp () in
      let jmp_frame = make_temp () in
      let val_addr = make_temp () in
      let trap_idx = List.length !handlers * 3 * 8 in
      `  mov.i64 {emit_temp exn_addr}, {emit_label handler}\n`;
      `  frame.i64 {emit_temp jmp_frame}, {emit_int (trap_idx + 0)}\n`;
      `  st.8 [{emit_temp jmp_frame}], {emit_temp exn_addr}\n`;
      `  jmp {emit_label label}\n`;
      `{emit_label handler}:\n`;
      `  mov.i64 {emit_temp val_addr}, caml_exn_val\n`;
      `  ld.8.i64 {emit_reg Proc.loc_exn_bucket}, [{emit_temp val_addr}] @caml_value\n`
    | Lpushtrap ->
      let handler =
        try Some (Hashtbl.find traps !current_block)
        with Not_found -> None
      in
      let trap_idx = List.length !handlers * 3 * 8 in
      handlers := handler :: !handlers;
      let exc_addr = emit_addr "caml_exception_pointer" in
      let jmp_frame = make_temp () in
      let sp_frame = make_temp () in
      let exc_frame = make_temp () in
      let exc_val = make_temp () in
      let sp_val = make_temp () in
      `  frame.i64 {emit_temp jmp_frame}, {emit_int (trap_idx + 0)}\n`;
      `  frame.i64 {emit_temp sp_frame}, {emit_int (trap_idx + 8)}\n`;
      `  frame.i64 {emit_temp exc_frame}, {emit_int (trap_idx + 16)}\n`;
      `  ld.8.u64 {emit_temp exc_val}, [{emit_temp exc_addr}]\n`;
      `  st.8 [{emit_temp exc_frame}], {emit_temp exc_val}\n`;
      `  mov.i64 {emit_temp sp_val}, $rsp\n`;
      `  st.8 [{emit_temp sp_frame}], {emit_temp sp_val}\n`;
      `  st.8 [{emit_temp exc_addr}], {emit_temp jmp_frame}\n`
    | Lpoptrap ->
      handlers := List.tl !handlers;
      let trap_idx = List.length !handlers * 3 * 8 in
      let exc_addr = emit_addr "caml_exception_pointer" in
      let exc_val = make_temp () in
      let exc_frame = make_temp () in
      `  frame.i64 {emit_temp exc_frame}, {emit_int (trap_idx + 16)}\n`;
      `  ld.8.i64 {emit_temp exc_val}, [{emit_temp exc_frame}]\n`;
      `  st.8 [{emit_temp exc_addr}], {emit_temp exc_val}\n`
    | Lraise(raise) ->
      (match raise with
      | _ when no_handler !handlers ->
        let exn = emit_addr "caml_raise_exception" in
        `  tcall.caml_raise {emit_temp exn}{emit_regs arg ", " ""}\n`;
      | _ ->
        let exn = emit_addr "caml_raise_exception" in
        `  invoke.caml_raise {emit_temp exn}{emit_regs arg ", " ""}{emit_handler !handlers}\n`;
        `  trap\n`;
      )
  in
  let is_terminator = function
    | Lreturn -> true
    | Lop (Itailcall_imm _) -> true
    | Lop (Itailcall_ind _) -> true
    | Lraise _ -> true
    | _ -> false
  in
  let rec emit = function
    | { desc = Lend } ->
      ()
    | { arg; res; desc; next = { desc = Lend } } when not (is_terminator desc) ->
        emit_instr arg res desc;
        `  mov.i64 $5000, 1\n`;
        `  ret     $5000\n`
    | instr ->
      emit_instr instr.arg instr.res instr.desc;
      emit instr.next
  in

  let rec sizeof_frame n m = function
    | { desc = Lend } -> m
    | { desc = Lpushtrap; next } -> sizeof_frame (n + 1) (max (n + 1) m) next
    | { desc = Lpoptrap; next } -> sizeof_frame (n - 1) m next
    | { next } -> sizeof_frame n m next
  in

  let frame_size = 8 * 3 * sizeof_frame 0 0 body in
  if frame_size <> 0 then
    `  .stack {emit_int frame_size}\n`;

  find_regs body;
  let arg_list = List.sort (fun (i1, _, _) (i2, _, _) -> compare i1 i2) (
      Hashtbl.fold (fun arg (idx, ty) acc -> (idx, arg, ty) :: acc) params []
  ) in
  `  .args 0`;
  arg_list |> List.iter (fun (_, _, ty) -> `, {emit_type ty}`);
  `\n`;
  arg_list |> List.iter
    (fun (idx, arg, ty) -> match ty with
    | Float ->      `  arg.f64 ${emit_int arg}, {emit_int idx}\n`
    | Val ->        `  arg.i64 ${emit_int arg}, {emit_int idx} @caml_value\n`
    | Addr | Int -> `  arg.i64 ${emit_int arg}, {emit_int idx}\n`
    );

  emit body;

  (* Emit bound error call *)
  !bound_error_labels |> List.iter (fun (handler, label) ->
    `{emit_label label}:\n`;
    let addr = emit_addr "caml_ml_array_bound_error" in
    match handler with
    | None   ->
      `  tcall.caml_raise {emit_temp addr}\n`
    | Some h ->
      `  invoke.caml_raise {emit_temp addr}, {emit_label h}\n`;
      `  trap\n`
    )

let data = function
  | [] ->
    ()
  | items ->
    `  .data caml\n`;
    `  .p2align 3\n`;
    items |> List.iter (function
    | Cstring s          -> emit_string_directive "  .ascii  " s
    | Cskip n            -> if n > 0 then `  .space   {emit_int n}\n`
    | Cdefine_symbol s   -> `{emit_symbol s}:\n`
    | Cglobal_symbol s   -> `  .globl   {emit_symbol s}\n`
    | Calign n           -> `  .p2align {emit_int n}\n`
    | Cint8 n            -> `  .byte    {emit_int n}\n`
    | Cint16 n           -> `  .short   {emit_int n}\n`
    | Cint32 n           -> `  .long    {emit_printf "0x%nx" n}\n`
    | Cint n             -> `  .quad    {emit_printf "0x%nx" n}\n`
    | Csymbol_address s  -> `  .quad    {emit_symbol s}\n`
    | Csingle f          -> `  .float   {emit_printf "0x%Lx" (Int64.bits_of_float f)}\n`
    | Cdouble f          -> `  .double  {emit_printf "0x%Lx" (Int64.bits_of_float f)}\n`
    );
    `  .end\n`;
    `\n`

let fundecl f =
  `  .text\n`;
  `  .globl {emit_symbol f.fun_name}\n`;
  `{emit_symbol f.fun_name}:\n`;
  `  .call\tcaml\n`;
  emit_linear f.fun_body;
  `\n`

let begin_assembly () =
  ()

let end_assembly () =
  ()

